plugins {
    id 'org.springframework.boot' version '2.5.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'com.google.cloud.tools.jib' version '3.1.4'
    id("com.epam.drill.integration.cicd") version "0.0.1-beta.arm.1"
}

group = 'com.realworld'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'


    implementation("io.jsonwebtoken:jjwt-api:0.11.2")
    runtimeOnly("io.jsonwebtoken:jjwt-impl:0.11.2")
    runtimeOnly("io.jsonwebtoken:jjwt-jackson:0.11.2")

    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true

    drill {
        appAgent {
            additionalParams = ["commitSha": System.getenv("DRILL_COMMIT_SHA")]
        }
    }
}

sourceSets {
    apiTest {
        java {
            compileClasspath += project.configurations.testCompileClasspath + main.output + test.output
            runtimeClasspath += project.configurations.testRuntimeClasspath + main.output + test.output
            srcDir 'apiTest'
        }
    }
}

tasks.register('runApiTests', Test) {
    description = 'Runs API tests.'
    group = 'verification'
    testClassesDirs = sourceSets.apiTest.output.classesDirs
    classpath = sourceSets.apiTest.runtimeClasspath
    useJUnitPlatform()

    drill {
        appAgent {
            enabled = true
        }
    }

    testLogging {
        showStandardStreams = true
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
    }

    doFirst {
        testLogging {
            showStandardStreams = true
        }
    }
}

jib {
    to {
        image = 'drill4j/realworld-spring-webflux'
    }
}

drill {
    drillApiUrl = System.getenv("DRILL_API_URL")
    drillApiKey = System.getenv("DRILL_API_KEY")
    groupId = "realworld"
    appId = "spring-webflux"
    packagePrefixes = ["com/realworld"]
    github {
        it.token = System.getenv("DRILL_BOT_GITHUB_TOKEN")
    }
    enableTestAgent {
        downloadUrl = System.getenv("DRILL_AUTOTEST_AGENT_URL")
    }
//    enableAppAgent {
//        version = "0.9.0-beta.arm.1"
//    }
}